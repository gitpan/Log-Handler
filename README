NAME
    Log::Handler - Log messages to several outputs.

SYNOPSIS
        use Log::Handler;

        my $log = Log::Handler->new();

        $log->add(file => {
            filename => 'file.log',
            mode     => 'append',
            maxlevel => 'debug',
            minlevel => 'warning',
            newline  => 1,
        });

        $log->warning("warn message");

    Or

        # init myapp logger with accessor LOG
        package MyApp;
        use Log::Handler myapp => 'LOG';

        LOG->add(screen => { maxlevel => 'info' });
        LOG->info("info message");

        # import myapp logger with accessor LOG
        package MyApp::Foo;
        use Log::Handler myapp => 'LOG';
        LOG->info("info message from MyApp::Foo");

        # get myapp logger with get_logger()
        package MyApp::Bar;
        use Log::Handler;
        my $log = Log::Handler->get_logger('myapp');
        $log->info("info message from MyApp::Bar");

DESCRIPTION
    The "Log::Handler" is a object oriented handler for logging, tracing and
    debugging. It is very easy to use and provides a simple interface for
    multiple output objects with lots of configuration parameters. You can
    easily filter the amount of logged information on a per-output base,
    define priorities and create patterns to format the messages.

    See the documentation for details.

LOG LEVELS
    There are eigth levels available:

        7   debug
        6   info
        5   notice
        4   warning, warn
        3   error, err
        2   critical, crit
        1   alert
        0   emergency, emerg

    "debug" is the highest and "emergency" is the lowest level.

    Level "debug" is the highest level because it basically says to log
    every peep.

METHODS
  new()
    Call "new()" to create a new log handler object.

        my $log = Log::Handler->new();

  add()
    Call "add()" to add a new output object.

    The following options are possible for the handler:

    maxlevel and minlevel
        With these options it's possible to set the log levels for your
        program.

        Example:

            maxlevel => 'error'
            minlevel => 'emergency'

            # or

            maxlevel => 'err'
            minlevel => 'emerg'

            # or

            maxlevel => 3
            minlevel => 0

        It's possible to set the log level as string or as number. The
        default setting for "maxlevel" is "warning" and the default setting
        for "minlevel" is "emergency".

        Example: If "maxlevel" is set to "warning" and "minlevel" to
        "emergency" then the levels "warning", "error", "critical", "alert"
        and "emergency" would be logged.

        You can set both to 8 or "nothing" if you want to disable the
        logging machine.

    timeformat
        The option "timeformat" is used to set the format for the
        placeholder %T. The string is converted with "POSIX::strftime". The
        default format is set to "%b %d %H:%M:%S" and looks like

            Feb 01 12:56:31

        If you would set the format to "%Y/%m/%d %H:%M:%S" it would looks
        like

            2007/02/01 12:56:31

    dateformat
        This options works like "timeformat". You can set a format that is
        used for the placeholder %D. It's just useful if you want to split
        the date and time:

            $log->add(file => {
                filename       => 'file.log',
                dateformat     => '%Y-%m-%d',
                timeformat     => '%H:%M:%S',
                message_layout => '%D %T %L %m',
            });

            $log->error("an error here");

        This looks like

            2007-02-01 12:56:31 ERROR an error here

        This option is not used by default.

    newline
        "newline" is a very helpful option. It let the logger appends a
        newline to the message if a newline doesn't exist.

            0 - do nothing (default)
            1 - append a newline if not exist

        Example:

            $log->add(
                screen => {
                    newline  => 1,
                    maxlevel => 'info',
                }
            );

            $log->info("message\n");
            $log->info("message");

        In both cases the message would be logged with a newline at the end.

    message_layout
        With this option it's possible to create your own message layout
        with different placeholders in "printf()" style. The available
        placeholders are:

            %L   Log level
            %T   Time or full timestamp (option timeformat)
            %D   Date (option dateformat)
            %P   PID
            %H   Hostname
            %U   User name
            %G   Group name
            %N   Newline
            %S   Program name
            %C   Caller - filename and line number
            %p   Caller - package name
            %f   Caller - file name
            %l   Caller - line number
            %s   Caller - subroutine name
            %r   Runtime in seconds since program start
            %t   Time measurement - replaced with the time since the last call of $log->$level
            %m   Message
            %%   Procent

        The default message layout is set to "%T [%L] %m".

        As example the following code

            $log->alert("foo bar");

        would log

            Feb 01 12:56:31 [ALERT] foo bar

        If you set "message_layout" to

            message_layout => '%T foo %L bar %m (%C)'

        and call

            $log->info("baz");

        then it would log

            Feb 01 12:56:31 foo INFO bar baz (script.pl, line 40)

        Traces will be appended after the complete message.

        You can create your own placeholders with the method
        "set_pattern()".

    message_pattern
        This option is just useful if you want to forward messages to output
        modules that needs the parts of a message as a hash reference - as
        example Log::Handler::Output::Forward, Log::Handler::Output::DBI,
        Log::Handler::Output::Email or Log::Handler::Output::Screen.

        The option expects a list of placeholders:

            # as a array reference
            message_pattern => [ qw/%T %L %H %m/ ]

            # or as a string
            message_pattern => '%T %L %H %m'

        The patterns will be replaced with real names as hash keys.

            %L   level
            %T   time
            %D   date
            %P   pid
            %H   hostname
            %U   user
            %G   group
            %N   newline
            %r   runtime
            %C   caller
            %p   package
            %f   filename
            %l   line
            %s   subroutine
            %S   progname
            %t   mtime
            %m   message

        Here a full code example:

            use Log::Handler;

            my $log = Log::Handler->new();

            $log->add(forward => {
                forward_to      => \&my_func,
                message_pattern => [ qw/%T %L %H %m/ ],
                message_layout  => '%m',
                maxlevel        => 'info',
            });

            $log->info('a forwarded message');

            # now you can access it

            sub my_func {
                my $msg = shift;
                print "Timestamp: $msg->{time}\n";
                print "Level:     $msg->{level}\n";
                print "Hostname:  $msg->{hostname}\n";
                print "Message:   $msg->{message}\n";
            }

    prepare_message
        "prepare_message" is useful if you want to do something with the
        message before it will be logged... maybe you want to create your
        own layout because message_layout doesn't meet your claim.

            $log->add(
                screen => {
                    newline => 1,
                    message_layout  => '%m (%t)',
                    message_pattern => [ qw/%T %L %H %m/ ],
                    prepare_message => \&format,
                }
            );

            $log->error("foo");
            $log->error("bar");
            $log->error("baz");

            sub format {
                my $m = shift;

                $m->{message} = sprintf('%-20s %-20s %-20s %s',
                    $m->{time}, $m->{level}, $m->{hostname}, $m->{message});
            }

        The output looks like

            Mar 08 15:14:20      ERROR                h1434036             foo (0.039694)
            Mar 08 15:14:20      ERROR                h1434036             bar (0.000510)
            Mar 08 15:14:20      ERROR                h1434036             baz (0.000274)

    priority
        With this option you can set the priority of your output objects.
        This means that messages will be logged at first to the outputs with
        a higher priority. If this option is not set then the default
        priority begins with 10 and will be increased +1 with each output.
        Example:

        We add a output with no priority

            $log->add(file => { filename => 'file1.log' });

        This output gets the priority of 10. Now we add another output

            $log->add(file => { filename => 'file2.log' });

        This output gets the priority of 11... and so on.

        Messages would be logged at first to the output with the priority of
        10 and then to the output with the priority of 11. Now you can add
        another output and set the priority to 1.

            $log->add(screen => { dump => 1, priority => 1 });

        Messages would be logged now at first to the screen.

    die_on_errors
        Set "die_on_errors" to 0 if you don't want that the handler dies on
        failed write operations.

            0 - to disable it
            1 - to enable it

        If you set "die_on_errors" to 0 then you have to controll it
        yourself.

            $log->info('info message') or die $log->errstr();

            # or Log::Handler->errstr()
            # or Log::Handler::errstr()
            # or $Log::Handler::ERRSTR

    filter_message
        With this option it's possible to set a filter. If the filter is set
        then only messages will be logged that match the filter. You can
        pass a regexp, a code reference or a simple string. Example:

            $log->add(file => {
                filename => 'file.log',
                mode     => 'append',
                newline  => 1,
                maxlevel => 6,
                filter_message => qr/log this/,
                # or
                # filter_message => 'log this',
                # filter_message => '^log only this$',
            });

            $log->info('log this');
            $log->info('but not that');

        If you pass your own code then you have to check the message
        yourself.

            $log->add(file => {
                filename => 'file.log',
                mode     => 'append',
                newline  => 1,
                maxlevel => 6,
                filter_message => \&my_filter
            });

            # return TRUE if you want to log the message, FALSE if not
            sub my_filter {
                my $msg = shift;
                $msg->{message} =~ /your filter/;
            }

        It's also possible to define a simple condition with matches. Just
        pass a hash reference with the options "matchN" and "condition".
        Example:

            $log->add(file => {
                filename => 'file.log',
                mode     => 'append',
                newline  => 1,
                maxlevel => 6,
                filter_message => {
                    match1    => 'log this',
                    match2    => qr/with that/,
                    match3    => '(?:or this|or that)',
                    condition => '(match1 && match2) || match3',
                }
            });

        NOTE that re-eval in regexes is not valid! Something like

            match1 => '(?{unlink("file.txt")})'

        would cause an error!

    filter_caller
        You can use this option to set a package name. Only messages from
        this packages will be logged.

        Example:

            my $log = Log::Handler->new();

            $log->add(screen => {
                maxlevel => 'info',
                newline  => 1,
                filter_caller  => qr/^Foo::Bar\z/,
                # or
                # filter_caller => '^Foo::Bar\z',
            });

            package Foo::Bar;
            $log->info('log this');

            package Foo::Baz;
            $log->info('but not that');

            1;

        This would only log the message from the package "Foo::Bar".

    except_caller
        This option is just the opposite of "filter_caller".

        If you want to log messages from all callers but "Foo::Bar":

            except_caller => qr/^Foo::Bar\z/

    alias
        You can set an alias if you want to get the output object later.
        Example:

            my $log = Log::Handler->new();

            $log->add(screen => {
                maxlevel => 7,
                alias    => 'screen-out',
            });

            my $screen = $log->output('screen-out');

            $screen->log(message => 'foo');

            # or in one step

            $log->output('screen-out')->log(message => 'foo');

    debug_trace
        You can activate a debugger that writes "caller()" informations for
        each active log level. The debugger is logging all defined values
        except "hints" and "bitmask". Set "debug_trace" to 1 to activate the
        debugger. The debugger is set to 0 by default.

    debug_mode
        There are two debug modes: line(1) and block(2) mode. The default
        mode is 1.

        The line mode looks like this:

            use strict;
            use warnings;
            use Log::Handler;

            my $log = Log::Handler->new()

            $log->add(file => {
                filename    => '*STDOUT',
                maxlevel    => 'debug',
                debug_trace => 1,
                debug_mode  => 1
            });

            sub test1 { $log->warning() }
            sub test2 { &test1; }

            &test2;

        Output:

            Apr 26 12:54:11 [WARNING]
               CALL(4): package(main) filename(./trace.pl) line(15) subroutine(main::test2) hasargs(0)
               CALL(3): package(main) filename(./trace.pl) line(13) subroutine(main::test1) hasargs(0)
               CALL(2): package(main) filename(./trace.pl) line(12) subroutine(Log::Handler::__ANON__) hasargs(1)
               CALL(1): package(Log::Handler) filename(/usr/local/share/perl/5.8.8/Log/Handler.pm) line(713) subroutine(Log::Handler::_write) hasargs(1)
               CALL(0): package(Log::Handler) filename(/usr/local/share/perl/5.8.8/Log/Handler.pm) line(1022) subroutine(Devel::Backtrace::new) hasargs(1) wantarray(0)

        The same code example but the debugger in block mode would looks
        like this:

               debug_mode => 2

        Output:

           Apr 26 12:52:17 [DEBUG]
              CALL(4):
                 package     main
                 filename    ./trace.pl
                 line        15
                 subroutine  main::test2
                 hasargs     0
              CALL(3):
                 package     main
                 filename    ./trace.pl
                 line        13
                 subroutine  main::test1
                 hasargs     0
              CALL(2):
                 package     main
                 filename    ./trace.pl
                 line        12
                 subroutine  Log::Handler::__ANON__
                 hasargs     1
              CALL(1):
                 package     Log::Handler
                 filename    /usr/local/share/perl/5.8.8/Log/Handler.pm
                 line        681
                 subroutine  Log::Handler::_write
                 hasargs     1
              CALL(0):
                 package     Log::Handler
                 filename    /usr/local/share/perl/5.8.8/Log/Handler.pm
                 line        990
                 subroutine  Devel::Backtrace::new
                 hasargs     1
                 wantarray   0

    debug_skip
        This option let skip the "caller()" informations the count of
        "debug_skip".

  HowTo use add()
    The method "add()" excepts 2 parts of options; the options for the
    handler and the options for the output module you want to use. The
    output modules got it's own documentation for all options. As example if
    you want to add a file-output then take a look into the documentation of
    Log::Handler::Output::File to see which options are available.

    There are different ways to add a new output to the handler. One way is
    to create the output object yourself and pass it with the handler
    options to "add()".

    Example:

        use Log::Handler;
        use Log::Handler::Output::File;

        # the handler options - how to handle the output
        my %handler_options = (
            timeformat      => '%Y/%m/%d %H:%M:%S',
            newline         => 1,
            message_layout  => '%T [%L] %S: %m',
            maxlevel        => 'debug',
            minlevel        => 'emergency',
            die_on_errors   => 1,
            debug_trace     => 0,
            debug_mode      => 2,
            debug_skip      => 0,
        );

        # the file options - how to handle the file
        my %file_options = (
            filename        => 'file.log',
            filelock        => 1,
            fileopen        => 1,
            reopen          => 1,
            mode            => 'append',
            autoflush       => 1,
            permissions     => '0660',
            utf8            => 1,
        );

        # create the file object
        my $file = Log::Handler::Output::File->new( \%file_options );

        # create a new handler object
        my $log = Log::Handler->new();

        # now we add the file object to the handler with the handler options
        $log->add( $file => \%handler_options );

    But it can be simplier! You can merge all options and pass them to
    "add()" in one step, you just need to tell the handler what do you want
    to add.

        use Log::Handler;

        my $log = Log::Handler->new();

        $log->add(

            # what do you want to add - a file output
            file => {

                # handler options
                timeformat      => '%Y/%m/%d %H:%M:%S',
                newline         => 1,
                message_layout  => '%T [%L] %S: %m',
                maxlevel        => 'debug',
                minlevel        => 'emergency',
                die_on_errors   => 1,
                debug_trace     => 0,
                debug_mode      => 2,
                debug_skip      => 0,

                # file options
                filename        => 'file.log',
                filelock        => 1,
                fileopen        => 1,
                reopen          => 1,
                mode            => 'append',
                autoflush       => 1,
                permissions     => '0660',
                utf8            => 1,

            }
        );

    The options will be splitted internal and you don't need to split it
    yourself, only if you want to do it yourself.

    Take a look to Log::Handler::Examples for more informations.

  Log level methods
    debug()
    info()
    notice()
    warning()
    error(), err()
    critical(), crit()
    alert()
    emergency(), emerg()

    The call of a log level method is very simple:

        $log->info("Hello World! How are you?");

    Or maybe:

        $log->info("Hello World!", "How are you?");

    Both calls would log - if level INFO is active:

        Feb 01 12:56:31 [INFO] Hello World! How are you?

  is_* methods
    is_debug()
    is_info()
    is_notice()
    is_warning(), is_warn()
    is_error(), is_err()
    is_critical(), is_crit()
    is_alert()
    is_emergency(), is_emerg()

    These twelve methods could be very useful if you want to kwow if the
    current level would log the message. All methods returns TRUE if the
    current set of "minlevel" and "maxlevel" would log the message and FALSE
    if not.

  Other level methods
    There exists some other level methods.

    For a full list take a look into the documentation of
    Log::Handler::Levels.

  output()
    Call "output($alias)" to get the output object that you added with the
    option "alias".

    It's possible to access a output directly:

        $log->output($alias)->log(message => 'booo');

    For more informations take a look to the option "alias".

  flush()
    Call "flush()" if you want to send flush to all outputs that can flush.

    Flush means to flush buffers and/or close and re-open outputs.

    If you want to send it only to some outputs you can pass the aliases.

        $log->flush(); # flush all
        $log->flush('foo', 'bar'); # flush only foo and bar

    If option "die_on_errors" is set to 0 then you can intercept errors
    with:

        $log->flush or die $log->errstr;

  errstr()
    Call "errstr()" if you want to get the last error message. This is
    useful if you set "die_on_errors" to 0 and the handler wouldn't die on
    failed write operations.

        use Log::Handler;

        my $log = Log::Handler->new();

        $log->add(file => {
            filename      => 'file.log',
            maxlevel      => 'info',
            mode          => 'append',
            die_on_errors => 0,
        });

        $log->info("Hello World!") or die $log->errstr;

    Or

        unless ( $log->info("Hello World!") ) {
            $error_string = $log->errstr;
            # do something with $error_string
        }

    The exception is that the handler dies in any case if the call of
    "new()" or "add()" fails because on missing or wrong settings!

  config()
    With this method it's possible to load your output configuration from a
    file.

        $log->config(config => 'file.conf');

    Or

        $log->config(config => {
            file => {
                default => {
                    newline       => 1,
                    debug_mode    => 2,
                    die_on_errors => 0
                },
                error_log => {
                    filename      => 'error.log',
                    maxlevel      => 'warning',
                    minlevel      => 'emerg',
                    priority      => 1
                },
                common_log => {
                    filename      => 'common.log',
                    maxlevel      => 'info',
                    minlevel      => 'emerg',
                    priority      => 2
                },
            }
        });

    The key "default" is used here to define default parameters for all file
    outputs. All other keys ("error_log", "common_log") are used as aliases.

    Take a look into the documentation of Log::Handler::Config for more
    informations.

  set_pattern()
    With this option you can set your own placeholders. Example:

        $log->set_pattern('%X', 'key_name', sub { 'value' });

        # or

        $log->set_pattern('%X', 'key_name', 'value');

    Then you can use this pattern in your message layout:

        $log->add(file => {
            filename        => 'file.log',
            mode            => 'append',
            message_layout  => '%X %m%N',
        });

    Or use it with "message_pattern":

        sub func {
            my $m = shift;
            print "$m->{key_name} $m->{message}\n";
        }

        $log->add(forward => {
            forward_to      => \&func,
            message_pattern => '%X %m',
        });

    Note: valid character for the key name are: "[%\w\-\.]+"

  set_level()
    With this method it's possible to change the log level at runtime.

    To change the log level it's neccessary to use a alias - see option
    "alias".

        $log->set_level(
            $alias => { # option alias
                minlevel => $new_minlevel,
                maxlevel => $new_maxlevel,
            }
        );

  create_logger()
    "create_logger()" is the same like "new()" but it creates a global
    logger.

        my $log = Log::Handler->create_logger('myapp');

    If you want to create more than one object you can call

        my @logger = Log::Handler->create_logger('myapp1', 'myapp2', ...);

  get_logger()
    With "get_logger()" it's possible to get a logger that was created with
    "create_logger()" or with

        use Log::Handler 'myapp';

    Just call

        my $log = Log::Handler->get_logger('myapp');

    Or

        my @logger = Log::Handler->get_logger('myapp1', 'myapp2', ...);

GLOBAL LOG HANDLER
    Since version 0.50 it's possible to define a application logger. This
    means to create a "Log::Handler" object and import it into all modules
    of your program.

        use Log::Handler alias => accessor;

    The accessor is created into the namespace of the caller and referenced
    to the alias.

    If the alias doesn't exists then a new "Log::Handler" object will be
    created.

    If no accesseor is set then no accessor is exported into the namespace
    of the caller.

    Example:

        package MyAPP;
        use strict;
        use warnings;
        use Log::Handler myapp => 'LOG';

        LOG->config(config => 'myapp.conf');
        LOG->info('hello world');

    Now you can access the logger after you import it.

        package MyAPP::Foo; 
        use Log::Handler myapp => 'LOG';

        LOG->info('message');

    If you want to import another accessor name ...

        package MyAPP::Bar;
        use Log::Handler myapp => 'MYLOG';

        MYLOG->info('message');

    Another way to get the logger is to call "get_logger()" if you don't
    want to create an accessor.

        package MyAPP::Foo::Bar;
        use Log::Handler;

        my $log = Log::Handler->get_logger('myapp');
        $log->info('message');

    For a little example you can take a look into the examples directory of
    the distribution (examples/logger/).

EXAMPLES
    Log::Handler::Examples

BENCHMARK
    Run examples/benchmark/benchmark.pl on a Dual-Core AMD Opteron(tm)
    Processor 1212 HE (1000 MHz)

        simple pattern output took     :  2 wallclock secs ( 2.29 usr +  0.02 sys =  2.31 CPU) @ 43290.04/s (n=100000)
        default pattern output took    :  4 wallclock secs ( 3.36 usr +  0.76 sys =  4.12 CPU) @ 24271.84/s (n=100000)
        complex pattern output took    :  6 wallclock secs ( 4.85 usr +  0.91 sys =  5.76 CPU) @ 17361.11/s (n=100000)
        message pattern output took    :  5 wallclock secs ( 4.31 usr +  0.82 sys =  5.13 CPU) @ 19493.18/s (n=100000)
        suppressed output took         :  0 wallclock secs ( 0.17 usr +  0.00 sys =  0.17 CPU) @ 588235.29/s (n=100000)
        filtered caller output took    :  5 wallclock secs ( 3.97 usr +  0.81 sys =  4.78 CPU) @ 20920.50/s (n=100000)
        suppressed caller output took  :  1 wallclock secs ( 1.29 usr +  0.00 sys =  1.29 CPU) @ 77519.38/s (n=100000)
        filtered messages output took  :  5 wallclock secs ( 3.91 usr +  0.84 sys =  4.75 CPU) @ 21052.63/s (n=100000)

EXTENSIONS
    Send me a mail if you have questions.

PREREQUISITES
    Prerequisites for all modules:

        Carp
        Data::Dumper
        Fcntl
        Params::Validate
        POSIX
        Time::HiRes
        Sys::Hostname

    Recommended modules:

        Config::General
        Config::Properties
        DBI
        IO::Socket
        Net::SMTP
        YAML

    Just for the test suite:

        File::Spec
        Test::More

EXPORTS
    No exports.

REPORT BUGS
    Please report all bugs to <jschulz.cpan(at)bloonix.de>.

AUTHOR
    Jonny Schulz <jschulz.cpan(at)bloonix.de>.

QUESTIONS
    Do you have any questions or ideas?

    MAIL: <jschulz.cpan(at)bloonix.de>

    IRC: irc.perl.org#perl

    If you send me a mail then add Log::Handler into the subject.

COPYRIGHT
    Copyright (C) 2007-2008 by Jonny Schulz. All rights reserved.

    This program is free software; you can redistribute it and/or modify it
    under the same terms as Perl itself.

DISCLAIMER OF WARRANTY
    BECAUSE THIS SOFTWARE IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY
    FOR THE SOFTWARE, TO THE EXTENT PERMITTED BY APPLICABLE LAW. EXCEPT WHEN
    OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES
    PROVIDE THE SOFTWARE "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
    EXPRESSED OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
    WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE
    ENTIRE RISK AS TO THE QUALITY AND PERFORMANCE OF THE SOFTWARE IS WITH
    YOU. SHOULD THE SOFTWARE PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL
    NECESSARY SERVICING, REPAIR, OR CORRECTION.

    IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING
    WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR
    REDISTRIBUTE THE SOFTWARE AS PERMITTED BY THE ABOVE LICENCE, BE LIABLE
    TO YOU FOR DAMAGES, INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL, OR
    CONSEQUENTIAL DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE THE
    SOFTWARE (INCLUDING BUT NOT LIMITED TO LOSS OF DATA OR DATA BEING
    RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR THIRD PARTIES OR A
    FAILURE OF THE SOFTWARE TO OPERATE WITH ANY OTHER SOFTWARE), EVEN IF
    SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE POSSIBILITY OF SUCH
    DAMAGES.

